import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

group publish_group
version publish_version

buildscript {
    repositories {
        mavenCentral()

        // KSP:
        google()
    }

    dependencies {
        classpath(libs.kotlin.gradle)
        classpath(libs.dokka.gradle)
        classpath(libs.kotlinx.binaryCompatibilityValidator)

        classpath(libs.ksp.gradle)
    }
}

apply plugin: 'binary-compatibility-validator'

allprojects {
    repositories {
        mavenCentral()

        // KSP:
        google()

        if (rootProject.snapshot_dependencies_enabled == "true") {
            logger.lifecycle("Adding snapshots repository")
            maven { url = "https://oss.sonatype.org/content/repositories/snapshots" }
        }
        if (rootProject.kotlin_dev_version_enabled == "true") {
            logger.lifecycle("Adding Kotlin dev repository")
            maven { url = "https://maven.pkg.jetbrains.space/kotlin/p/kotlin/dev" }
        }
    }

    pluginManager.withPlugin("org.jetbrains.kotlin.jvm") {
        project.tasks.withType(KotlinCompile.class).configureEach {
            kotlinOptions {
                jvmTarget = JavaVersion.VERSION_1_8.toString()
                freeCompilerArgs += ['-progressive']
            }
        }
    }
}
