buildscript {
    apply from: "properties.gradle"
    ext.isCi = System.getenv().get("CI") == "true"
    ext.resolvedJavaVersion = System.getenv().get("ci_java_version") ?: JavaVersion.VERSION_1_8.toString()

    repositories {
        if (isCi) {
            logger.lifecycle("Resolving buildscript Poko dependencies from MavenLocal")
            exclusiveContent {
                forRepository { mavenLocal() }
                filter { includeGroup publish_group }
            }
        }
        mavenCentral()
        google()
    }

    dependencies {
        classpath("$publish_group:$publish_gradle_plugin_artifact:$publish_version")
        classpath(libs.kotlin.gradle)
        classpath(libs.android.gradle)
    }
}

allprojects {
    repositories {
        if (isCi) {
            logger.lifecycle("Resolving Poko dependencies from MavenLocal")
            exclusiveContent {
                forRepository { mavenLocal() }
                filter { includeGroup publish_group }
            }
        }
        mavenCentral()
    }
}
