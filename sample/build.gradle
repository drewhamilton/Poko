buildscript {
    apply from: "properties.gradle"
    ext.isCi = System.getenv().get("CI") == "true"
    ext.ciJavaVersion = System.getenv().get("ci_java_version")
    ext.resolvedJavaVersion = ciJavaVersion ?: JavaVersion.VERSION_11.toString()
    logger.lifecycle("Targeting Java version $resolvedJavaVersion")
    ext.kotlinJvmTarget = resolvedJavaVersion == "8" ? "1.8" : resolvedJavaVersion

    repositories {
        if (isCi) {
            logger.lifecycle("Resolving buildscript Poko dependencies from MavenLocal")
            exclusiveContent {
                forRepository { mavenLocal() }
                filter { includeGroup publish_group }
            }
        }
        mavenCentral()
        google()
    }

    dependencies {
        classpath("$publish_group:$publish_gradle_plugin_artifact:$publish_version")
        classpath(libs.kotlin.gradle)

        // AGP requires Java 11; skip Android stuff on tests for lower JDKs
        if (ciJavaVersion == null || Integer.valueOf(ciJavaVersion) >= 11) {
            classpath(libs.android.gradle)
        } else {
            logger.lifecycle("Testing on JDK $ciJavaVersion; skipping AGP")
        }
    }
}

allprojects { project ->
    repositories {
        if (isCi) {
            logger.lifecycle("Resolving $project Poko dependencies from MavenLocal")
            exclusiveContent {
                forRepository { mavenLocal() }
                filter { includeGroup publish_group }
            }
        }
        mavenCentral()
    }
}
