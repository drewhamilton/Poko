plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
}
apply plugin: "dev.drewhamilton.poko"

poko {
    pokoAnnotation.set "dev.drewhamilton.poko.sample.compose.Poko"
    enabled.set true
}

android {
    compileSdkVersion 32

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 32
    }

    compileOptions {
        sourceCompatibility resolvedJavaVersion
        targetCompatibility resolvedJavaVersion
    }

    kotlinOptions {
        jvmTarget = kotlinJvmTarget.toString().replace("JVM_", "").replace("_", ".")
        freeCompilerArgs += [
            "-progressive",
            "-P",
            "plugin:androidx.compose.compiler.plugins.kotlin:suppressKotlinVersionCompatibilityCheck=true",
        ]
    }

    buildFeatures {
        compose true

        // Disable unused AGP features
        buildConfig false
        aidl false
        renderScript false
        resValues false
        shaders false
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.androidx.compose.compiler.get()
    }
}

dependencies {
    implementation(libs.androidx.compose.runtime)

    testImplementation(libs.junit)
    testImplementation(libs.truth)
}

repositories {
    google()
    if (android.composeOptions.kotlinCompilerExtensionVersion.contains("dev")) {
        logger.lifecycle("Adding Compose compiler dev repository")
        maven { url "https://androidx.dev/storage/compose-compiler/repository" }
    }
}
